module geography {

	cfunc-prefix geo;

	gobject Location {}
	
	gerror NavigationError {
		INVALID_LATITUDE,
		INVALID_LONGITUDE,
		GPS_NOT_AVAILABLE
	}
	
	gflags PosDeterm {
		GPS,
		IP_ADDRESS,
		MOBILE
	}
	
	typedecl RouteCallback;
	
	ginterface Route {
	
		methods {
		
			create
				<- callback: RouteCallback [callback(user-data-param=user_data,
					scope=notified)] 
				<- user_data: any [allow-none];
		
			get_way_points
				-> list(Location) [transfer-full, callee-allocates];
				
			set_way_points
				<- num_points: integer
				<- points: list(Location) [const, array(length-param=num_points)];
		
		}
	
	}
	
}

module base {

	genum AnimalCatg { UNDEFINED=99, FISH=100, BIRD, MAMMAL }

	gobject Animal [abstract] {
	
		implements Movable;
	
		properties {
		
			category {
				type = enumeration,
				gtype = gtypeof(AnimalCatg),
				default = AnimalCatg.UNDEFINED,
				access = read & write & init-only
			}
		
		}
		
		public methods {

			constructor
				<- category: AnimalCatg;
				
			talk [abstract]
				<- message: string;
						
		}
		
		protected attributes {
		
			internal_name: string;
		
		}
		
		private attributes {
			
			category: AnimalCatg;
			
		}
	
	}
	
	ginterface Movable {
	
		methods {
	
			goto
				<- location: geography/Location [const, transfer-none];
				
		} 

	    properties {
	    
	    	current-location {
	    		description = "current location",
	    		access = read,
	    		type = object,
	    		gtype = gtypeof(geography/Location)
	    	}
	    
	    }
	    
	    signals {
	    
	    	location-reached
	    		<- location: geography/Location;
	    
	    }
	
	}

}
	
gobject Dog {

	inherits from base/Animal;
	
	public methods {
	
		Dog
			category <- base/AnimalCatg.MAMMAL
			<- name: string [const];
			
		bark [final];
		
	}
	
}
	
gobject Human [final] {
	
	inherits from base/Animal;
	
	signals {
	
		hello;
	
		weight-changed [has-default-handler]
			<- new_weight: float
			<- old_weight: float;
			
		has-married
			<- partner: Human
			<- info: string;
					
	}
		
	properties {

		first-name {
			description = "first name",
			type = string,
			access = read & write & init-only
		}

		last-name {
			description = "last name",
			type = string,
			access = read & write & init-only
		}
		
		full-name {
			description = "full name",
			type = string,
			access = read
		}
		
		is-male {
			description = "gender info",
			type = boolean,
			default = true,
			access = read & write & init
		}
		
		weight {
			description = "weight in kg",
			type = float,
			access = read & write & init
		}
		
		dress_code {
			type = byte,
			max = 100
		}
		
		first-child {
			description = "first child",
			type = object,
			gtype = gtypeof(Human),
			access = read & write
		}
			 
	}
		
	public methods {
		
		Human 
			category <- base/AnimalCatg.MAMMAL
			first-name <- first_name: string [const]
			last-name <- last_name: string [const]
			weight <- weight: float;
			
		override talk;
		
		think
			<- subject: any;
			
		override goto of ginterface base/Movable;
				
	}
	
	private methods {
	
		whisper;
	
	}
	
	private attributes {
		
		first_name: string;
		last_name: string;
	
	}

	private attributes {
		
		weight: float;
	
	}
		
}
